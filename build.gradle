group = 'com.ashurex.pokemon'
version = '0.0.1-SNAPSHOT'

apply plugin: 'java'

description = """Pokemon GO Java Bot"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
	maven { url "https://jitpack.io" }	
    mavenLocal()
}

subprojects {
	apply plugin: 'java'
	repositories {
	    mavenCentral()
    	maven { url "https://jitpack.io" }
    	mavenLocal()
	}
}

project(':PokeGOAPI-Java') {
	javadoc {
		onlyIf {
			project.hasProperty("doDocs")
		}
	}
}

project(':geocalc') {
	dependencies {
		compile 'log4j:log4j:1.2.17'
		testCompile  'junit:junit:4.12'
	}	
	test {
		onlyIf {
			project.hasProperty("doTests")
		}
	}
}
	
dependencies {
	compile project(':geocalc')
	compile project(':PokeGOAPI-Java')
    compile 'com.google.maps:google-maps-services:0.1.15'
    compile 'com.squareup.okhttp3:okhttp:3.4.0-RC1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'org.codehaus.janino:janino:2.7.8'
	testCompile  'junit:junit:4.12'
}

jar {
	from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    
    manifest {
		attributes  'Implementation-Title': rootProject.name,
                    'Implementation-Version': version,        
	                'Built-Date': new Date(),
    	            'Built-JDK': System.getProperty('java.version'),
					'Main-Class': 'com.ashurex.pokemon.BotMain'
    }
}
